plugins {
    id 'com.github.dcendents.android-maven' version '2.1'
    id 'com.jfrog.bintray' version '1.8.4'
}
apply plugin: 'com.android.library'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 3
        versionName "0.0.3"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha02'
    implementation 'com.google.firebase:firebase-auth:16.1.0'
    implementation 'com.google.firebase:firebase-core:16.0.7'
    implementation project(':content')
}

ext {
    bintrayRepo = 'firebit'
    bintrayName = 'auth'

    pubGroupId = 'com.owellox.firebit'
    pubArtifactId = 'auth'

    gitUrl = 'https://github.com/owellox/firebit.git'
    webUrl = 'https://github.com/owellox/firebit.git'

    libName = 'Authentication'
    libDesc = 'Firebase authentication made easier.'
    libVersion = '0.0.3'

    devId = 'owellox'
    devName = 'Owellox'
    devEmail = 'owellox@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    licenseList = ['Apache-2.0']
}

group = pubGroupId
version = libVersion

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId pubGroupId
            artifactId pubArtifactId
            name libName
            description libDesc
            url webUrl

            licenses {
                license {
                    name licenseName
                    url licenseUrl
                }
            }
            developers {
                developer {
                    id devId
                    name devName
                    email devEmail
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url webUrl
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.key')
    configurations = ['archives']

    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libDesc
        websiteUrl = webUrl
        vcsUrl = gitUrl
        licenses = licenseList
        dryRun = false
        publish = true
        override = false
        publicDownloadNumbers = true
        version {
            desc = libDesc
        }
    }
}